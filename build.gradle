buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'

version = '1.16.5-2.1.6'
group = 'meldexun.entity_culling'
archivesBaseName = 'EntityCulling'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'official', version: '1.16.5'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            taskName 'runClient EC 1.16'
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'

            mods {
                entity_culling {
                    source sourceSets.main
                }
            }
        }

        server {
            taskName 'runServer EC 1.16'
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'

            mods {
                entity_culling {
                    source sourceSets.main
                }
            }
        }

        data {
            taskName 'runData EC 1.16'
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'
            args '--mod', 'entity_culling', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                entity_culling {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    includedDependencies

    implementation.extendsFrom(includedDependencies)
}

repositories {
    ivy {
        url 'https://github.com/'

        patternLayout {
            artifact '[organisation]/[module]/releases/download/v[revision]/[module]-[revision].[ext]'
        }

        metadataSources { artifact() } 
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.6'
    includedDependencies 'meldexun:ReflectionUtil:1.0.0@jar'
    includedDependencies 'meldexun:RaytraceUtil:1.0.1@jar'
}

jar {
    from {
        configurations.includedDependencies.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
